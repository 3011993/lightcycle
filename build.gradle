buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.5.0'
    classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.0'
  }
}

task wrapper(type: Wrapper) {
  description 'Creates the gradle wrapper.'
  gradleVersion '2.4'
}

allprojects {
  repositories {
    mavenLocal()
    mavenCentral()
  }
}

ext {
  minSdkVersion = 8
  targetSdkVersion = 23
  compileSdkVersion = 23
  buildToolsVersion = '23.0.1'
  sourceCompatibilityVersion = JavaVersion.VERSION_1_7
  targetCompatibilityVersion = JavaVersion.VERSION_1_7
}

ext.deps = [
  // Android
  android           : 'com.google.android:android:4.1.1.4',
  support_v4        : 'com.google.android:support-v4:r7',
  appcompat_v7      : 'com.android.support:appcompat-v7:23.2.0',

  // Processor
  javawriter        : 'com.squareup:javawriter:2.1.2',

  // Test dependencies
  junit             : 'junit:junit:4.12',
  mockito           : 'org.mockito:mockito-all:1.10.19',
  compiletesting    : 'com.google.testing.compile:compile-testing:0.7',
]

// configure Java projects
[":lightcycle-api", ":lightcycle-processor"].each { name ->
  project(name) {

    apply plugin: 'provided-base'
    apply plugin: 'java'
    apply plugin: 'maven'

    group = GROUP
    version = VERSION_NAME
// TODO name = POM_ARTIFACT_ID

    sourceCompatibility = rootProject.ext.sourceCompatibilityVersion
    targetCompatibility = rootProject.ext.targetCompatibilityVersion

    task sourcesJar(type: Jar, dependsOn: classes) {
      classifier = 'sources'
      from sourceSets.main.allSource
    }

    task testJar(type: Jar) {
      classifier = 'tests'
      from sourceSets.test.output
      include '**/test/**/*.class'
    }

    artifacts {
      archives jar
      archives sourcesJar
      archives testJar
    }

    uploadArchives {
      repositories {
        mavenDeployer {
          repository(url: RELEASE_REPOSITORY_URL)
          snapshotRepository(url: SNAPSHOT_REPOSITORY_URL)
        }
      }
    }
  }
}
